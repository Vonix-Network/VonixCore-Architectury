plugins {
    id 'com.gradleup.shadow'
}

loom {
    forge {
        mixinConfig "vonixcore.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-forge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')

    // Explicitly include dependencies for shadowing
    shadowBundle("org.javacord:javacord:3.8.0") {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    shadowBundle "com.squareup.okhttp3:okhttp:4.12.0"
    shadowBundle "com.squareup.okio:okio-jvm:3.9.0"
    shadowBundle "com.squareup.okio:okio:3.9.0"
    shadowBundle "com.fasterxml.jackson.core:jackson-databind:2.17.0"
    shadowBundle "com.fasterxml.jackson.core:jackson-core:2.17.0"
    shadowBundle "com.fasterxml.jackson.core:jackson-annotations:2.17.0"
    shadowBundle "io.vavr:vavr:0.10.4"
    shadowBundle "com.neovisionaries:nv-websocket-client:2.14"
    shadowBundle "com.zaxxer:HikariCP:5.1.0"
    shadowBundle "org.bouncycastle:bcprov-jdk18on:1.78.1"
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'build_time', System.currentTimeMillis()

    filesMatching('META-INF/mods.toml') {
        expand version: inputs.properties.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
    
    // Exclude logging libraries that conflict with Minecraft's module system
    exclude 'org/slf4j/**'
    exclude 'org/apache/logging/**'
    
    relocate 'org.javacord', 'network.vonix.vonixcore.shadow.javacord'
    relocate 'okhttp3', 'network.vonix.vonixcore.shadow.okhttp3'
    relocate 'okio', 'network.vonix.vonixcore.shadow.okio'
    relocate 'com.fasterxml.jackson', 'network.vonix.vonixcore.shadow.jackson'
    relocate 'com.zaxxer.hikari', 'network.vonix.vonixcore.shadow.hikari'
    relocate 'io.vavr', 'network.vonix.vonixcore.shadow.vavr'
    relocate 'com.neovisionaries.ws', 'network.vonix.vonixcore.shadow.nvwebsocket'
    relocate 'org.bouncycastle', 'network.vonix.vonixcore.shadow.bouncycastle'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
