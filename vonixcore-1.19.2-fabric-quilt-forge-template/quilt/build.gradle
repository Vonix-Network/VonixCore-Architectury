plugins {
    id 'com.gradleup.shadow'
}

repositories {
    maven { url 'https://maven.quiltmc.org/repository/release/' }
}

architectury {
    platformSetupLoomIde()
    loader('quilt')
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentQuilt.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "org.quiltmc:quilt-loader:$rootProject.quilt_loader_version"

    // Quilt Standard Libraries and QSL.
    modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:$rootProject.quilted_fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation("dev.architectury:architectury-fabric:$rootProject.architectury_api_version") {
        // We must not pull Fabric Loader and Fabric API from Architectury Fabric.
        exclude group: 'net.fabricmc'
        exclude group: 'net.fabricmc.fabric-api'
    }

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionQuilt')

    // Database
    shadowBundle 'com.zaxxer:HikariCP:5.1.0'
    shadowBundle 'org.xerial:sqlite-jdbc:3.45.1.0'

    // Discord Integration
    shadowBundle('org.javacord:javacord:3.8.0') {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    shadowBundle 'com.squareup.okhttp3:okhttp:4.12.0'
    shadowBundle 'com.squareup.okio:okio-jvm:3.9.0'
    shadowBundle 'com.squareup.okio:okio:3.9.0'

    // Jackson
    shadowBundle 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    shadowBundle 'com.fasterxml.jackson.core:jackson-core:2.17.0'
    shadowBundle 'com.fasterxml.jackson.core:jackson-annotations:2.17.0'

    // Javacord transitive
    shadowBundle 'io.vavr:vavr:0.10.4'
    shadowBundle 'com.neovisionaries:nv-websocket-client:2.14'

    // Crypto
    shadowBundle 'com.codahale:xsalsa20poly1305:0.11.0'
    shadowBundle 'org.bouncycastle:bcprov-jdk18on:1.78.1'
}

processResources {
    inputs.property 'group', project.group
    inputs.property 'version', project.version
    inputs.property 'build_time', System.currentTimeMillis()

    filesMatching('quilt.mod.json') {
        expand group: project.group, version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
    
    // Relocate packages to avoid conflicts with other mods
    relocate 'com.zaxxer.hikari', 'network.vonix.vonixcore.shadow.hikari'
    // Do NOT relocate org.sqlite - breaks JNI
    relocate 'org.javacord', 'network.vonix.vonixcore.shadow.javacord'
    relocate 'okhttp3', 'network.vonix.vonixcore.shadow.okhttp3'
    relocate 'okio', 'network.vonix.vonixcore.shadow.okio'
    relocate 'com.fasterxml.jackson', 'network.vonix.vonixcore.shadow.jackson'
    relocate 'io.vavr', 'network.vonix.vonixcore.shadow.vavr'
    relocate 'com.neovisionaries.ws', 'network.vonix.vonixcore.shadow.nvwebsocket'
    relocate 'org.bouncycastle', 'network.vonix.vonixcore.shadow.bouncycastle'

    mergeServiceFiles()
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
